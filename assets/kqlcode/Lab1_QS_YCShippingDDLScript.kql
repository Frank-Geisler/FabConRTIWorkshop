.execute database script <|
.alter table RawShippingMsgs folder 'Bronze Layer'
.create-or-alter function processShippingMsgs()
{
    RawShippingMsgs
    | extend xmltojson = parse_xml(data)
    | evaluate bag_unpack(xmltojson)
    | evaluate bag_unpack(ShippingEvent)
    | extend DestinationAddressName = tostring(DestinationAddress.Name)
            , DestinatonAddressStreet = tostring(['DestinationAddress']['Street'])
            , DestinatonAddressCity = tostring(['DestinationAddress']['City'])
            , DestinatonAddressZip = tostring(['DestinationAddress']['Zip'])
            , DestinatonAddressCountry = tostring(['DestinationAddress']['Country'])
            , DestinatonAddressLatitude = toreal(['DestinationAddress']['Latitude'])
            , DestinatonAddressLongitude = toreal(['DestinationAddress']['Longitude'])
            , SourceDCCity = tostring(['SourceDistributionCenter']['City'])
            , SourceDCCountry = tostring(['SourceDistributionCenter']['Country'])
            , SourceDCLatitude = toreal(['SourceDistributionCenter']['Latitude'])
            , SourceDCLongitude = toreal(['SourceDistributionCenter']['Longitude'])
    | project-away DestinationAddress, SourceDistributionCenter, data
}
.create table ShippingEvents (EventTime:datetime, OrderNumber:guid, Provider:string, ShippingContents:dynamic, Status:string, WeightKg:dynamic, DestinationAddressName:string, DestinatonAddressStreet:string, DestinatonAddressCity:string, DestinatonAddressZip:string, DestinatonAddressCountry:string, DestinatonAddressLatitude:real, DestinatonAddressLongitude:real, SourceDCCity:string, SourceDCCountry:string, SourceDCLatitude:real, SourceDCLongitude:real) with (folder = 'Silver Layer')
.alter table ShippingEvents policy update
```[{
    "IsEnabled": true,
    "Source": "RawShippingMsgs",
    "Query": "processShippingMsgs",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```