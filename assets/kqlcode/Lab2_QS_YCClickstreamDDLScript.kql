.execute database script <|
.alter table RawClickstreamData folder 'Bronze Layer'
.create-or-alter function extractAddToCartPayload ()
{
    RawClickstreamData
    | where event_type == "add_to_cart"
    | project product_category = tostring(['click_path'][1])
            , product_id = tostring(payload.product_id)
            , quantity_added = toint(payload.quantity_added)
            , ip_address = tostring(client_info.ip)
            , browser = tostring(client_info.browser)
            , os = tostring(client_info.os)
            , device = tostring(client_info.device)
            , timestamp
            , user_id
            , session_id
            , event_id
    | extend country = tostring((geo_info_from_ip_address(ip_address).country))
            , state = tostring((geo_info_from_ip_address(ip_address).state))
            , city = tostring((geo_info_from_ip_address(ip_address).city))
            , latitude = toreal((geo_info_from_ip_address(ip_address).latitude))
            , longitude = toreal((geo_info_from_ip_address(ip_address).longitude))
}
.create-or-alter function extractViewProductPayload ()
{
    RawClickstreamData
    | where event_type == "view_product"
    | project product_id = tostring(payload.product_id)
        , product_category = tostring(payload.category)
        , price = toreal(payload.price)
        , sneaker_category = tostring(payload.sneaker_category)
        , ip_address = tostring(client_info.ip)
        , browser = tostring(client_info.browser)
        , os = tostring(client_info.os)
        , device = tostring(client_info.device)
        , country
        , timestamp
        , user_id
        , session_id
        , event_id
}
.create-or-alter function extractCreateAccountPayload ()
{
    RawClickstreamData
    | where event_type == "create_account"
    | project account_type = tostring(payload.account_type)
            , ip_address = tostring(client_info.ip)
            , browser = tostring(client_info.browser)
            , os = tostring(client_info.os)
            , device = tostring(client_info.device)
            , country
            , timestamp
            , user_id
            , session_id
            , event_id
}
.create-or-alter function extractNewsletterPayload ()
{
    RawClickstreamData
    | where event_type in ("unsubscribe_mailing_list", "subscribe_mailing_list")
    | project event_type
            , subscribed = tobool(payload.subscribed)
            , ip_address = tostring(client_info.ip)
            , browser = tostring(client_info.browser)
            , os = tostring(client_info.os)
            , device = tostring(client_info.device)
            , country
            , timestamp
            , user_id
            , session_id
            , event_id
}
.create-or-alter function extractBrowseCategoryPayload()
{
    RawClickstreamData
    | where event_type == "browse_category"
    | project product_category = tostring(payload.category)
            , sneaker_category = tostring(payload.sneaker_category)
            , ip_address = tostring(client_info.ip)
            , browser = tostring(client_info.browser)
            , os = tostring(client_info.os)
            , device = tostring(client_info.device)
            , country
            , timestamp
            , user_id
            , session_id
            , event_id
}
.create table Clickstream_AddToCart (product_category:string, product_id:string, quantity_added:int, ip_address:string, browser:string, os:string, device:string, timestamp:datetime, user_id:guid, session_id:guid, event_id:guid, country:string, state:string, city:string, latitude:real, longitude:real) with (folder ="Silver Layer")
.create table Clickstream_BrowseCategory (product_category:string, sneaker_category:string, ip_address:string, browser:string, os:string, device:string, country:string, timestamp:datetime, user_id:guid, session_id:guid, event_id:guid) with (folder ="Silver Layer")
.create table Clickstream_CreateAccount (account_type:string, ip_address:string, browser:string, os:string, device:string, country:string, timestamp:datetime, user_id:guid, session_id:guid, event_id:guid) with (folder ="Silver Layer")
.create table Clickstream_Newsletter (event_type:string, subscribed:bool, ip_address:string, browser:string, os:string, device:string, country:string, timestamp:datetime, user_id:guid, session_id:guid, event_id:guid) with (folder ="Silver Layer")
.create table Clickstream_ViewProduct (product_id:string, product_category:string, price:real, sneaker_category:string, ip_address:string, browser:string, os:string, device:string, country:string, timestamp:datetime, user_id:guid, session_id:guid, event_id:guid) with (folder ="Silver Layer")
.alter table Clickstream_Newsletter policy update
```[{
    "IsEnabled": true,
    "Source": "RawClickstreamData",
    "Query": "extractNewsletterPayload",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```
.alter table Clickstream_AddToCart policy update
```[{
    "IsEnabled": true,
    "Source": "RawClickstreamData",
    "Query": "extractAddToCartPayload",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```
.alter table Clickstream_CreateAccount policy update
```[{
    "IsEnabled": true,
    "Source": "RawClickstreamData",
    "Query": "extractCreateAccountPayload",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```
.alter table Clickstream_BrowseCategory policy update
```[{
    "IsEnabled": true,
    "Source": "RawClickstreamData",
    "Query": "extractBrowseCategoryPayload",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```
.alter table Clickstream_ViewProduct policy update
```[{
    "IsEnabled": true,
    "Source": "RawClickstreamData",
    "Query": "extractViewProductPayload",
    "IsTransactional": false,
    "PropagateIngestionProperties": true
}]```